<semantic-table
  id="risk-table"
  query='
PREFIX cidoc:  <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX crmsci: <http://www.ics.forth.gr/isl/CRMsci/>
PREFIX rdfs:  <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd:   <http://www.w3.org/2001/XMLSchema#>

SELECT
  ?subject
  ?event_name
  ?magnitudo
  ?bgColor
  ?bdColor
WHERE {
  VALUES ?subject { <[[this]]> }

  ################ EVENT + MIN/MAX VALUES ################
  {
    SELECT
      ?subject ?event ?event_name
      (SAMPLE(?minExtracted) AS ?minNum)
      (SAMPLE(?maxExtracted) AS ?maxNum)
    WHERE {
      ?subject ^cidoc:P31_has_modified ?event .
      ?event a cidoc:E5_Event ;
             rdfs:label ?event_name ;
             cidoc:P39i_was_measured_by ?measurement .

      ?measurement a crmsci:S21_Measurement ;
                   cidoc:P2_has_type <https://w3id.org/sirius/type/magnitude-score> .

      ?measurement cidoc:P40_observed_dimension ?dim .
      ?dim cidoc:P90_has_value ?valRaw .
      BIND(xsd:decimal(?valRaw) AS ?val)

      # Flags
      BIND(CONTAINS(STR(?dim), "magnitude_score_min") AS ?isMin)
      BIND(CONTAINS(STR(?dim), "magnitude_score_max") AS ?isMax)

      # Only assign numbers when the flag is true
      BIND(IF(?isMin, ?val, xsd:decimal(0)) AS ?minExtracted)
      BIND(IF(?isMax, ?val, xsd:decimal(0)) AS ?maxExtracted)
    }
    GROUP BY ?subject ?event ?event_name
  }

  ################ GLOBAL RANGE ################
  {
  SELECT
    ?subject
    (MIN(?vReal) AS ?globalMin)
    (MAX(?vReal) AS ?globalMax)
  WHERE {
    ?subject ^cidoc:P31_has_modified ?ev .
    ?ev cidoc:P39i_was_measured_by ?meas .
    ?meas cidoc:P40_observed_dimension ?d .

    # Only keep TRUE min/max dimensions
    FILTER(
      CONTAINS(STR(?d), "magnitude_score_min") ||
      CONTAINS(STR(?d), "magnitude_score_max")
    )

    ?d cidoc:P90_has_value ?raw .
    BIND(xsd:decimal(?raw) AS ?vReal)
  }
  GROUP BY ?subject
}

  ############ MAGNITUDE AVG ############
  BIND(
    IF(?minNum > 0 && ?maxNum > 0,
        (?minNum + ?maxNum)/2,
        IF(?minNum > 0, ?minNum,
           IF(?maxNum > 0, ?maxNum, xsd:decimal(0))))
    AS ?magnitudo
  )

  ############ COLOR LOGIC ############
  BIND(xsd:decimal(?globalMax - ?globalMin) AS ?range)
  BIND(IF(?range > 0, (?magnitudo - ?globalMin)/?range, 0.5) AS ?pct)

  # flip scale: green→yellow→red
  BIND(ROUND(120 * (1 - ?pct)) AS ?hue)

  BIND(CONCAT("hsl(", STR(?hue), ",70%,85%)") AS ?bgColor)
  BIND(CONCAT("hsl(", STR(?hue), ",70%,40%)") AS ?bdColor)
}
ORDER BY DESC(?magnitudo)
'
  column-configuration='[
    { "displayName": "Event",           "cellTemplate": "{{> eventCell}}" },
    { "displayName": "Magnitude (avg)", "cellTemplate": "{{> magnitudoCell}}" }
  ]'
  options='{"showFilter": true, "resultsPerPage": 10, "stickyHeader": true}'>

  <template id="eventCell">
    {{#if event_name.value}}
      <div class="pill">{{event_name.value}}</div>
    {{else}}
      <div class="empty">—</div>
    {{/if}}
  </template>

  <template id="magnitudoCell">
    {{#if magnitudo.value}}
      <div class="pill magnitude-cell"
           style="background-color: {{bgColor.value}}; border-color: {{bdColor.value}};">
        {{magnitudo.value}}
      </div>
    {{else}}
      <div class="empty">—</div>
    {{/if}}
  </template>

</semantic-table>

<style>
  #risk-table .pill { display:inline-block; padding:.2rem .5rem;
    border:1px solid #e5e7eb; border-radius:999px; }
  #risk-table .empty{ text-align:center; opacity:.5; }
  #risk-table .magnitude-cell { font-weight: 600; }
</style>
